apply plugin: 'java'

group = 'org.greenrobot'
archivesBaseName = 'greendao'
version = rootProject.version
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':greendao-api')
    compileOnly 'com.google.android:android:4.1.1.4'
    compileOnly 'com.google.android:android-test:4.1.1.4'
    compileOnly 'com.google.android:annotations:4.1.1.4'
    compileOnly 'com.google.android:support-v4:r7'

    compileOnly 'io.reactivex:rxjava:1.1.8'

    compileOnly files('libs/sqlcipher.jar')
}

apply from: rootProject.file("gradle/publish.gradle")

javadoc {
    failOnError = false
    title = " greenDAO ${version} API"
    options.bottom = 'Available under the Apache License, Version 2.0 - <i>Copyright &#169; 2011-2020 <a href="https://greenrobot.org/">greenrobot.org</a>. All Rights Reserved.</i>'

    excludes = ['org/greenrobot/dao/internal', 'org/greenrobot/dao/Internal*']
    def srcApi = project(':greendao-api').file('src/main/java/')
    if (!srcApi.directory) throw GradleScriptException("Not a directory: ${srcApi}", null)
    source += srcApi
    doLast {
        copy {
            from '../javadoc-style'
            into "build/docs/javadoc/"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

artifacts {
    // jar added by Java plugin.
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'greenDAO'
                description = 'greenDAO is a light and fast ORM for Android'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                // Include other metadata as needed
                url = 'https://github.com/greenrobot/greenDAO'
                scm {
                    url = 'https://github.com/greenrobot/greenDAO'
                    connection = 'scm:git@github.com:greenrobot/greenDAO.git'
                    developerConnection = 'scm:git@github.com:greenrobot/greenDAO.git'
                }
                developers {
                    developer {
                        id = 'greenrobot'
                        name = 'greenrobot'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/greenrobot/greenDAO/issues'
                }
                organization {
                    name = 'greenrobot'
                    url = 'https://greenrobot.org'
                }
            }
        }
    }
}
